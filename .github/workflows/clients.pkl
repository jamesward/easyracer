// pkl eval --multiple-file-output-path . clients.pkl

import "package://pkg.pkl-lang.org/github.com/jamesward/pklgha/pklgha@0.0.27#/GitHubAction.pkl"
//import "../../../pklgha/src/GitHubAction.pkl"


class Run {
  `runs-on`: String?
  steps: Listing<GitHubAction.Step>
}

clients = new Mapping<String, Run> {
  ["dotnet"] = new {
    steps = GitHubAction.Dotnet.testSteps(null, "3.1.x", "./dotnet/EasyRacer.Tests")
  }

  ["elm-worker"] = new {
    steps = (GitHubAction.Elm.setupElm("0.19.1")) {
      new {
        run = """
          echo Compiling Elm sources...
          for num in {1..11}; do
            elm make --optimize --output=app/EasyRacer/Scenario$num.js src/EasyRacer/Scenario$num.elm
          done
          npm install --no-fund
          npm install -g --save-dev --no-fund jest
          npm test
          """
      }
    }
  }

  ["fsharp-reactive"] = new {
    steps = GitHubAction.Dotnet.testSteps(null, "9.0.x", "./fsharp-reactive/EasyRacer.Tests")
  }

  ["go-conc"] = new {
    steps = GitHubAction.Go.testSteps(null, "go-conc/go.mod")
  }

  ["go-stdlib"] = new {
    steps = GitHubAction.Go.testSteps(null, "go-stdlib/go.mod")
  }

  ["haskell-async"] = new {
    steps {
      new {
        uses = "actions/checkout@v4"
      }
      new {
        uses = "haskell-actions/setup@v2"
        with {
          ["ghc-version"] = "9.6"
        }
      }
      new {
        run = "cabal test"
      }
    }
  }

  ["java-cf"] = new {
    steps = GitHubAction.Java.mavenTestSteps("--batch-mode --no-transfer-progress verify", "java-cf", "graalvm", "23")
  }

  ["java-loom"] = new {
    steps = GitHubAction.Java.gradleTestSteps(null, "java-loom")
  }

  ["java-jox"] = new {
    steps = GitHubAction.Java.gradleTestSteps(null, "java-jox")
  }

  ["javascript-stdlib"] = new {
    steps = GitHubAction.Node.testSteps(null, null, "javascript-stdlib/package-lock.json", "javascript-stdlib/package.json")
  }

  ["kotlin-arrow"] = new {
    steps = GitHubAction.Java.gradleTestSteps(null, "kotlin-arrow")
  }

  ["kotlin-coroutines"] = new {
    steps = GitHubAction.Java.gradleTestSteps(null, "kotlin-coroutines")
  }

  ["kotlin-flow"] = new {
    steps = GitHubAction.Java.gradleTestSteps(null, "kotlin-flow")
  }

  ["kotlin-splitties"] = new {
    steps = GitHubAction.Java.gradleTestSteps(null, "kotlin-splitties")
  }

  ["ocaml-cohttp-eio"] = new {
    steps = GitHubAction.OCaml.testSteps(null, "5.1")
  }

  ["ocaml-cohttp-lwt"] = new {
    steps = new {
      new GitHubAction.Step {
        name = "Install libev"
        run = "sudo apt install libev4 libev-dev"
        `working-directory` = "./"
      }
      ...GitHubAction.OCaml.testSteps(null, "5.1")
    }
  }

  ["python-aiohttp"] = new {
    steps = GitHubAction.Nix.runSteps("pytest")
  }

  ["python-aiohttp-taskgroup"] = new {
    steps = GitHubAction.Nix.runSteps("pytest")
  }

  ["python-httpx-asyncio"] = new {
    steps = GitHubAction.Python.testSteps(null, null)
  }

  ["python-httpx-trio"] = new {
    steps = GitHubAction.Nix.runSteps("pytest")
  }

  ["python-rxpy"] = new {
    steps = GitHubAction.Nix.runSteps("pytest")
  }

  ["rust-tokio"] = new {
    steps {
      new {
        uses = "actions/checkout@v4"
      }
      new {
        run = "rustup update"
      }
      new {
        run = "cargo test"
      }
    }
  }

  ["scala-akkastreams"] = new {
    steps = GitHubAction.Scala.sbtTestSteps(null, "./scala-akkastreams", "21")
  }

  ["scala-ce3"] = new {
    steps = GitHubAction.Scala.sbtTestSteps(null, "./scala-ox", "21-ea")
  }

  ["scala-gears"] = new {
    steps = GitHubAction.Scala.sbtTestSteps(null, "./scala-gears", "21")
  }

  ["scala-kyo"] = new {
    steps = GitHubAction.Scala.sbtTestSteps(null, "./scala-kyo", null)
  }

  ["scala-ox"] = new {
    steps = GitHubAction.Scala.sbtTestSteps(null, "./scala-ox", "21-ea")
  }

  ["scala-soundness"] = new {
    steps = GitHubAction.Scala.sbtTestSteps(null, "./scala-soundness", "21")
  }

  ["scala-zio"] = new {
    steps = GitHubAction.Scala.sbtTestSteps(null, "./scala-zio", null)
  }

  local swiftSteps = new Listing<GitHubAction.Step> {
    new {
      uses = "actions/checkout@v4"
    }
    new {
      uses = "atomicjar/testcontainers-cloud-setup-action@v1"
      with {
        ["token"] = "${{ secrets.TC_CLOUD_TOKEN }}"
      }
    }
    new {
      name = "Set up Docker socket"
      run = """
        brew install socat
        DOCKER_PORT=$(grep tc.host ~/.testcontainers.properties | cut -d: -f3)
        sudo socat UNIX-LISTEN:/var/run/docker.sock,reuseaddr,fork TCP-CONNECT:127.0.0.1:$DOCKER_PORT &
        sleep 2
        sudo chmod a+rw /var/run/docker.sock
        """
    }
    new {
      name = "Select Xcode version"
      run = "sudo xcode-select -s '/Applications/Xcode_16.3.app/Contents/Developer'"
    }
    new {
      name = "Run scenarios"
      run = "swift test -c release --disable-swift-testing"
    }
  }

  ["swift-async"] = new {
    `runs-on` = "macos-26"
    steps = swiftSteps
  }

  ["swift-combine"] = new {
    `runs-on` = "macos-26"
    steps = swiftSteps
  }

  ["swift-dispatch"] = new {
    steps {
      new {
        uses = "actions/checkout@v4"
      }
      new {
        run = "swift test -c release"
      }
    }
  }

  ["typescript-effect"] = new {
    steps = GitHubAction.Node.testSteps(null, null, "typescript-effect/package-lock.json", "typescript-effect/package.json")
  }
}

function workflow(client: String, s: Run): GitHubAction = new GitHubAction {
  on {
    workflow_run {
      workflows {
        "scenario-server"
      }
      types {
        "completed"
      }
    }
    push {
      paths {
        "\(client)/**"
        ".github/workflows/\(client).yaml"
      }
    }
    pull_request {
      paths {
        "\(client)/**"
        ".github/workflows/\(client).yaml"
      }
    }
  }

  jobs {
    ["test"] = new {
      `runs-on` = s.`runs-on` ?? "ubuntu-24.04"
      `if` = "${{ github.event.workflow_run == null || github.event.workflow_run.conclusion == 'success' }}"
      steps = s.steps
      defaults {
        run {
          `working-directory` = client
        }
      }
    }
  }
}

output {
  files = clients
    .toMap()
    .map((key, v) -> Pair("\(key).yaml", new FileOutput {
      text = "# Generated file. DO NOT EDIT.\n" + super.text
      value = workflow(key, v)
      renderer = new YamlRenderer { }
    }))
    .toMapping()
}

